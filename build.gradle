group 'com.fkistner'
version '0.8.0-alpha' + (System.getenv("BUILD_NUMBER") ? "-" + System.getenv("BUILD_NUMBER") : '')

buildscript {
    ext.kotlin_version = '1.1.3-2'

    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.15"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
        classpath "gradle.plugin.edu.sc.seis:macAppBundle:2.1.6"
        // classpath 'edu.sc.seis.gradle:launch4j:2.3.0'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'antlr'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'edu.sc.seis.macAppBundle'
// apply plugin: 'edu.sc.seis.launch4j'


sourceCompatibility = 1.8

repositories {
    jcenter()
}

// Work around https://github.com/gradle/gradle/issues/820
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

dependencies {
    antlr "org.antlr:antlr4:4.7"
    compile "org.antlr:antlr4-runtime:4.7"
    compile "com.fifesoft:rsyntaxtextarea:2.6.1"
    compile "com.fifesoft:autocomplete:2.6.1"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    testCompile 'junit:junit:4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = project.sourceCompatibility
}
compileTestKotlin {
    kotlinOptions.jvmTarget = project.sourceCompatibility
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-no-listener", "-long-messages"]
}
compileKotlin.dependsOn generateGrammarSource



dokka {
    outputFormat = 'html' 
    outputDirectory = javadoc.destinationDir
    reportNotDocumented = true
    jdkVersion = 8

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/fkistner/SouthQuay/blob/master/src/main/kotlin"
        suffix = "#L"
    }
}



mainClassName = 'com.fkistner.SouthQuay.EntrypointKt'

distTar.enabled = false
distZip.enabled = false
shadowDistTar.enabled = false
shadowDistZip.enabled = false
createDmg.enabled = false
assemble.dependsOn createAppZip

shadowJar {
    classifier = null
    destinationDir = distsDir
}

macAppBundle {
    appName = 'South Quay'
    bundleIdentifier = 'com.fkistner.SouthQuay'
    mainClassName = project.mainClassName
    dmgName = project.name + '-mac-' + project.version
}


// launch4j {
//     mainClassName = project.mainClassName
//     dontWrapJar = true
// }
