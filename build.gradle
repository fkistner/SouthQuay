group 'com.fkistner'

buildscript {
    ext.kotlin_version = '1.1.3-2'

    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.edu.sc.seis:macAppBundle:2.1.6"
        // classpath 'edu.sc.seis.gradle:launch4j:2.3.0'
    }
}


apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'antlr'
apply plugin: 'application'
apply plugin: 'edu.sc.seis.macAppBundle'
// apply plugin: 'edu.sc.seis.launch4j'


sourceCompatibility = 1.8

repositories {
    jcenter()
}

// Work around https://github.com/gradle/gradle/issues/820
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

dependencies {
    antlr "org.antlr:antlr4:4.7"
    compile "org.antlr:antlr4-runtime:4.7"
    compile "com.fifesoft:rsyntaxtextarea:2.6.1"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    testCompile 'junit:junit:4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = project.sourceCompatibility
}
compileTestKotlin {
    kotlinOptions.jvmTarget = project.sourceCompatibility
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-no-listener", "-long-messages"]
}
compileKotlin.dependsOn generateGrammarSource


mainClassName = 'com.fkistner.SouthQuay.EntrypointKt'

distTar.enabled = false
createDmg.enabled = false
assemble.dependsOn createAppZip


macAppBundle {
    bundleIdentifier = 'com.fkistner.SouthQuay'
    mainClassName = project.mainClassName
    volumeName = project.name
    dmgName = project.name + '-mac'
}


// launch4j {
//     mainClassName = project.mainClassName
//     dontWrapJar = true
// }
